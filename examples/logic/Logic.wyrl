// This defines the basic rules for reducing logical expressions.

// ====================================================================
// Variables
// ====================================================================

term Var(string)

// ====================================================================
// Bool
// ====================================================================

term True
term False
define Bool as True | False

// ====================================================================
// Not
// ====================================================================

term Not(BExpr)

reduce Not(Bool b)
name "Not(Bool)":
    => False, if b == True
    => True

reduce Not(Not(* x))
name "Not(Not)":
    => x

reduce Not(And{BExpr... xs})
name "Not(And)":
    => let ys = { Not(x) | x in xs }
       in Or(ys)

reduce Not(Or{BExpr... xs})
name "Not(Or)":
    => let ys = { Not(x) | x in xs }
       in And(ys)

// ====================================================================
// And
// ====================================================================

term And{BExpr...}

reduce And{BExpr x}
name "And{x}":
    => x

reduce And{Bool b, BExpr... xs}
name "And{Bool,...}":
    => False, if b == False
    => True, if |xs| == 0
    => And (xs)

reduce And{And{BExpr... xs}, BExpr... ys}
name "And{And,...}":
    => And (xs ++ ys)

reduce And{Not(BExpr x), BExpr y, BExpr... ys}
name "And{Not,...}":
    => False, if x == y

reduce And{Or{BExpr... xs}, BExpr... ys}
name "And{Or,...}":
    => let ys = { And(x ++ ys) | x in xs }
       in Or(ys)

// ====================================================================
// Or
// ====================================================================

term Or{BExpr...}

reduce Or{BExpr x}
name "Or{x}":
    => x

reduce Or{Bool b, BExpr... xs}
name "Or{Bool,...}":
    => True, if b == True
    => False, if |xs| == 0
    => Or (xs)

reduce Or{Not(BExpr x), BExpr y, BExpr... ys}
name "Or{Not,...}":
    => True, if x == y

reduce Or{Or{BExpr... xs}, BExpr... ys}
name "Or{Or,...}":
    => Or (xs ++ ys)

// ====================================================================
// Expressions & Values
// ====================================================================

define Value as Bool
define BExpr as Bool | Not | And | Or | Var





