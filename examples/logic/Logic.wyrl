// This defines the basic rules for reducing logical expressions.

// ====================================================================
// Variables
// ====================================================================

term Var(string)

// ====================================================================
// Bool
// ====================================================================

term True
term False
define Bool as True | False

// ====================================================================
// Not
// ====================================================================

term Not(BExpr)

@name("Not(Bool)")
reduce Not(Bool b):
    => False, if b == True
    => True

@name("Not(Not)")
reduce Not(Not(* x)):
    => x

@name("Not(And)")
reduce Not(And{BExpr... xs}):
    => let ys = { Not(x) | x in xs }
       in Or(ys)

@name("Not(Or)")
reduce Not(Or{BExpr... xs}):
    => let ys = { Not(x) | x in xs }
       in And(ys)

// ====================================================================
// And
// ====================================================================

term And{BExpr...}

@name("And{x}")
reduce And{BExpr x}:
    => x

@name("And{Bool,...}")
reduce And{Bool b, BExpr... xs}:
    => False, if b == False
    => True, if |xs| == 0
    => And (xs)

@name("And{And,...}")
reduce And{And{BExpr... xs}, BExpr... ys}:
    => And (xs ++ ys)

@name("And{Not,...}")
reduce And{Not(BExpr x), BExpr y, BExpr... ys}:
    => False, if x == y

@name("And{Or,...}")
reduce And{Or{BExpr... xs}, BExpr... ys}:
    => let ys = { And(x ++ ys) | x in xs }
       in Or(ys)

// ====================================================================
// Or
// ====================================================================

term Or{BExpr...}

@name("Or{x}")
reduce Or{BExpr x}:
    => x

@name("Or{Bool,...}")
reduce Or{Bool b, BExpr... xs}:
    => True, if b == True
    => False, if |xs| == 0
    => Or (xs)

@name("Or{Not,...}")
reduce Or{Not(BExpr x), BExpr y, BExpr... ys}:
    => True, if x == y

@name("Or{Or,...}")
reduce Or{Or{BExpr... xs}, BExpr... ys}:
    => Or (xs ++ ys)

// ====================================================================
// Expressions & Values
// ====================================================================

define Value as Bool
define BExpr as Bool | Not | And | Or | Var





